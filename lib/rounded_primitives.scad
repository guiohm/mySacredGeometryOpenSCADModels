// Rounded primitives for openscad
// (c) 2013 Wouter Robers

// Syntax example for a rounded block
//translate([-15,0,10]) rcube([20,20,20],2);

// Syntax example for a rounded cylinder
//translate([15,0,10]) rcylinder(r1=15,r2=10,h=20,b=2);




module rcube(Size=[20,20,20],b=2) {
    hull() {
        for (x=[-(Size[0]/2-b),(Size[0]/2-b)]){
            for (y=[-(Size[1]/2-b),(Size[1]/2-b)]) {
                for (z=[-(Size[2]/2-b),(Size[2]/2-b)]) {
                    translate([x,y,z])
                    sphere(b);
                }
            }
        }
    }
}


module rcylinder(r1=10, r2=10, h=10, b=2, center=false, fn=20) {
    t = center ? -h/2 : 0;
    translate([0,0,t])
    hull() {
        rotate_extrude()
            translate([r1-b,b,0])
            circle(r= b, $fn=fn);
        rotate_extrude()
            translate([r2-b, h-b, 0])
            circle(r= b, $fn=fn);
    }
}
